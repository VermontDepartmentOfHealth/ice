/**
 * Copyright (C) 2019 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package org.cdsframework.ice.v1_1_0

import java.util.Date
import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.Schedule
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.supportingdata.BaseDataRecommendationReason
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl 

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext
global org.cdsframework.ice.service.Schedule schedule

			
// Pneumococcal Adult Series: Set Recommended Interval of 5yrs from non-valid PPSV Adult Target Dose 1 shot administered before the absolute minimum age to recommended PPSV vaccine.
// Note that PPSV is always the recommended routine vaccine for the adult series.
rule "Pneumococcal Adult Series: Recommended Earliest/Recommended Interval of 5yrs from prior PPSV shot, administered before the absolute minimum age, to the next target dose of PPSV" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the Series is == 6
		There is an administered shot $shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the dose number in the Series is <= 6
			- the vaccine administered a member of ("ICE33","ICE109")
			- make note of the date this Shot was administered as $dateAdultPPSVAdministeredBelowAbsMinAge
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the vaccine administered a member of ("ICE33","ICE109")
			- make note of the date this Shot was administered as $dateOtherAdultPPSVAdministeredBelowAbsMinAge
			- the date $dateOtherAdultPPSVAdministeredBelowAbsMinAge > $dateAdultPPSVAdministeredBelowAbsMinAge
	then
		Add "5y" to $dateAdultPPSVAdministeredBelowAbsMinAge and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Earliest Forecast Date for $oRecommendation to $dtCalculated
		Set the Recommendation Recommended Forecast Date for $oRecommendation to $dtCalculated
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Pneumococcal Adult Series: Set Recommended Interval of 5yrs from non-valid PPSV Adult Target Dose 1 shot administered before the absolute minimum age to recommended PPSV vaccine. 
// Note that PPSV is always the recommended routine vaccine for the adult series.
rule "Pneumococcal Adult Series: Recommended Earliest/Recommended Interval of 1yr from prior PCV target dose to the next target dose of Adult PPSV" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the Series is == 6
		There is an administered shot $shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the dose number in the Series is <= 6
			- the vaccine administered a member of ("ICE133","ICE152")
			- make note of the date this Shot was administered as $dateMostRecentPCVAdministered
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the vaccine administered a member of ("ICE133","ICE152")
			- make note of the date this Shot was administered as $dateOtherRecentPCVAdministered
			- the date $dateOtherRecentPCVAdministered > $dateMostRecentPCVAdministered
	then
		Add "1y" to $dateMostRecentPCVAdministered and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Earliest Forecast Date for $oRecommendation to $dtCalculated
		Set the Recommendation Recommended Forecast Date for $oRecommendation to $dtCalculated
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series		
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
 END Rules for Adult Recommending Intervals from shots administered below absolute minimum age for Dose 6 (Adult Dose 1)
/************************************************************************************************************************************************************************************/

// If the patient is >= 65 years old, and a PCV (CVX 133, 152) shot was not administered, ICE will return Descriptive Text:
// "Consider PCV13 based on shared clinical decision making for adults 65 years or older. MMWR. 2019;68(46);1069–1075.", along with the vaccine group recommendation. 
rule "Pneumococcal Adult Series: If patient >= 65yrs and has not had a PCV shot, return supplemental text to consider PCV shared clinical decision making" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the vaccine administered a member of ("ICE133","ICE152")
		The patient information $patientInformation must be known to complete writing this rule
			- make note of the date as $dtDateAtAge65 when the patient is "65y" of age
			- the date evalTime >= $dtDateAtAge65
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Supplemental Text for $oRecommendation to "Consider PCV13 based on shared clinical decision making for adults 65 years or older. MMWR. 2019;68(46);1069–1075."
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series		
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
PPSV->PCV Interval Interaction Rules in Child Series

* For Patients < 2 years of age:
    + Recommended Interval: if a PPSV23 (CVX 33) shot has been given, the recommended interval is 0 days from the PPSV23 to the next target dose of PCV in the child series. 
      For the purpose of recommendation, ignore the PPSV23 shot.If PPSV is the recommended vaccine, then the recommended interval is 5 years
      
* For Patients >=2 years and < 5 years of age:
	+ Recommended Interval: if a PPSV23 (CVX 33) shot has been given to a patient >= 2 years old and < 5 years old and the patient will be < 5 years old at the recommended 
	  due date, the recommended interval is 56 days from the PPSV23 to the next target dose of PCV in the child series. If the patient will be >= 5 years old and < 65 years at 
	  the recommended due date then the Recommendation is Conditional and the reason code is HIGH_RISK. 
/************************************************************************************************************************************************************************************/

// Recommended Interval: if a PPSV23 (CVX 33) shot has been given, the recommended interval is 0 days from the PPSV23 to the next target dose of PCV in the child series. 
// For the purpose of recommendation, ignore the PPSV23 shot.
rule "Pneumococcal: if the patient < 2yrs, if a PPSV23 (CVX 33) shot has been given, recommended interval of 0 days from the PPSV23 to the next target dose of PCV" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the series is <= 5
			- make note of the Last Shot Administered in the Series as $lastAdministeredShot
			- make note of the Date that the most recent Shot was Administered as $dateLastAdministeredShot
		There is an administered shot $shot
			- that is the same shot as $lastAdministeredShot
			- the vaccine administered is "ICE33"
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $dateLastAdministeredShot < "2y"
	then
		Mark the shot $shot as Ignored
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Recommended Interval: if a PPSV23 (CVX 33) shot has been given, the recommended interval is 0 days from the PPSV23 to the next target dose of PCV in the child series. 
// For the purpose of recommendation, ignore the PPSV23 shot.
rule "Pneumococcal: if the patient >= 2yrs and < 5 years, if a PPSV23 (CVX 33) shot has been given, recommended interval of 56 days from the PPSV23 to the next target dose of PCV" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the Last Shot Administered in the Series as $lastAdministeredShot
			- make note of the Date that the most recent Shot was Administered as $dateLastAdministeredShot
		There is an administered shot $shot
			- that is the same shot as $lastAdministeredShot
			- the vaccine administered is "ICE33"
			- Make note of the date this shot was administered as $dtAdministrationDate
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $dateLastAdministeredShot >= "2y" && elapsed time between $dtBirthDate and $dateLastAdministeredShot < "5y"
	then
		Add "56d" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Include a Recommendation as $r with Recommended Forecast Date $dtCalculated for consideration in the final Forecast of the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
END PPSV->PCV Interval Interaction Rules in Child Series
/*************************************************************************************************************************************************************************************



/*************************************************************************************************************************************************************************************
Rules for Recommending at the CVX Code vs. Vaccine Group Level

* When recommending in the Child Series, always recommend at the CVX level; vaccine recommended should be PCV 13 (CVX 133).
* When recommending in the Adult Series, 
    + If the patient is < 65 years old, then recommend at the vaccine group level.
    + If the patient is >= 19 years old, then always recommend at the CVX level: recommend PPSV (CVX 33) for adult dose 1. 
/************************************************************************************************************************************************************************************/

rule "Pneumococcal Child Series: Recommend at the vaccine level in child series; vaccine recommended should be PCV 13 (CVX 133)" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "postRecommendationCheck"
	salience 100
	when
		There is a Series $targetSeries
			- a Forecast for the Series has been made and a Recommendation Date has been determined
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- post processing on the Series forecast has not already been run
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the series is < 6
			- make note of the Recommendation Date as $recommendationDate
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and evalTime < "5y" && elapsed time between $birthDate and $recommendationDate < "5y"
	then
		Set the Recommended Vaccine for the Forecast in the Series $targetSeries to "ICE133"
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


//////////////
// Pneumococcal Adult Series: If the patient is < 19 years old, then recommend at the vaccine group level. This is the default behavior - no custom rule needed.
// If the patient >= 19 years old, recommend at the vaccine level-- a PPSV (CVX 33).
//////////////
rule "Pneumococcal Adult Series: If the patient is >= 19 years old, always recommend PPSV (CVX 33) for adult target dose 1" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 100
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the Series is == 6
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and evalTime >= "19y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation to "ICE33"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END Rules for Recommending at the Vaccine vs. Vaccine Group Level
/************************************************************************************************************************************************************************************/



/*************************************************************************************************************************************************************************************
PCV 13 Rule: If a child is complete for the series, has not had a dose of PCV 13 (CVX 133), is currently < 5 years old, and will be < 5 years old at the recommended due date, 
then a dose of PCV 13 (CVX 133) is recommended. Recommended interval is 8 weeks
/************************************************************************************************************************************************************************************/

// If a child is complete for the series, has had one or more doses but not had a dose of PCV 13 (CVX 133), is currently < 5 yrs old and will be < 5 years old at the recommended due date, 
// then a dose of PCV 13 (CVX 133) is recommended. Recommended interval = 8 weeks from the previous shot.
rule "Pneumococcal Child Series: Recommend PCV13 dose if series complete if no prior VALID PCV13 doses, is <5 yrs old and will be less than 5yrs old upon recommendation date" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 450
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the Series is == 5
			- the Number of Administered Shots is > 0
			- make note of the date that the most recent shot was administered as $shotDate			
		Verify that the Count of Doses Administered in Series $targetSeries with Vaccine "ICE133" is == 0
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime < "5y"
		Confirm elapsed time between $dtBirthDate and (calculate date from addition of $shotDate with TimePeriod "8w") < "5y"
	then
		Add "8w" to $shotDate and make note of the newly calculated date as $dtCalculated
		Include a recommendation as $recommendation with recommended forecast date $dtCalculated for consideration in the final forecast of the series $targetSeries
		Set the Recommendation Vaccine for $recommendation to "ICE133"
		Mark forecasting of the Series $targetSeries Complete
		Insert an IceFact SupportedFactConcept._PNEUMOCOCCAL_RECOMMENDED_CHILD_RECEIVE_PCV13_DOSE.getConceptCodeValue() with TargetSeries $targetSeries into working memory
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END PCV 13 Rule
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
Recommendation Rules based on Age
* If a patient is < 5 years and has completed the Child series (including meeting the PCV 13 Rule), then the Recommendation is Not Recommended and the reason code is COMPLETE_HIGH_RISK.
* If a patient is >= 5 years (or will be >= 5 years at the recommended due date) and < 19 years, and they have completed the Child Series, then the Recommendation is Conditional and 
* the reason code is COMPLETE_HIGH_RISK; otherwise the Recommendation is Conditional and the reason code is HIGH_RISK.
 
* If a patient is >= 5 years (or will be >= 5 years at the recommended due date) and < 19 years, then the Recommendation is Conditional and the reason code is HIGH_RISK.
/************************************************************************************************************************************************************************************/

// If patient is < 5 years and has completed the Child series (including meeting the PCV 13 Rule), then the Recommendation is Not Recommended and the reason code is COMPLETE_HIGH_RISK. Has a lower
// salience then the PCV13 rule
rule "Pneumococcal Child Series: if patient < 5yrs completed the Child series (including the PCV13 rule), recommend Not Recommended with reason COMPLETE_HIGH_RISK" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 425
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the Series is <= 6
			- the effective dose number in the Series is > 4
		There is an administered shot $shot
			- the shot belongs to the Series $targetSeries
			- the vaccine administered is "ICE133"
			- that has already been evaluated and whose Shot Validity is VALID
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime < "5y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE_HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Mark Forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If patient is >= 5 years and < 19 years and the series is not complete, then the Recommendation is Conditional and the reason code is HIGH_RISK.
rule "Pneumococcal Series: if patient *is* >= 5yrs and < 19yrs of age and the series *is not* complete, then recommend Conditional/HIGH_RISK" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 200
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		There does not exist an IceFact
			- that has finding SupportedFactConcept._PNEUMOCOCCAL_CHILD_SERIES_COMPLETE.conceptCodeValue
			- that has associated series $targetSeries
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "5y" && elapsed time between $dtBirthDate and evalTime < "19y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// If patient is >= 5 years and < 19 years and the series is not complete, then the Recommendation is Conditional and the reason code is HIGH_RISK.
rule "Pneumococcal Series: if patient *is* >= 5yrs and < 19yrs of age and the series *is* complete, then recommend Conditional/COMPLETE_HIGH_RISK" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 200
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		There exists an IceFact
			- that has finding SupportedFactConcept._PNEUMOCOCCAL_CHILD_SERIES_COMPLETE.conceptCodeValue
			- that has associated series $targetSeries
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "5y" && elapsed time between $dtBirthDate and evalTime < "19y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE_HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Post Recommendation Check: If the series is not complete and the patient will be >= 5 years at the recommended due date and < 19 years at the recommended due date, 
// then the Recommendation is Conditional and the reason code is HIGH_RISK. This rule will only fire if there is a recommendation date attached to the final recommendation 
// (i.e. - if the recommendation status is RECOMMENDED or RECOMMENDED_IN_FUTURE) and with the added conditional recommendation, ICE will override that recommendation with 
// the conditionally recommended (as per general ICE rules)
rule "Pneumococcal: if patient **will be** >= 5 years at the recommended due date and < 19 years and the series *is not* complete, then recommend Conditional/High Risk" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "postRecommendationCheck"
	when
		There is a Series $targetSeries
			- a Forecast for the Series has been made and a Recommendation Date has been determined
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the Recommendation Date as $recommendationDate
			- post processing on the Series forecast has not already been run
		There does not exist an IceFact
			- that has finding SupportedFactConcept._PNEUMOCOCCAL_CHILD_SERIES_COMPLETE.conceptCodeValue
			- that has associated series $targetSeries
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and $recommendationDate >= "5y" && elapsed time between $birthDate and $recommendationDate < "19y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Post Recommendation Check: If the series is complete and the patient will be >= 5 years at the recommended due date and < 65 years at the recommended due date, 
// then the Recommendation is Conditional and the reason code is HIGH_RISK. This rule will only fire if there is a recommendation date attached to the final recommendation 
// (i.e. - if the recommendation status is RECOMMENDED or RECOMMENDED_IN_FUTURE) and with the added conditional recommendation, ICE will override that recommendation with 
// the conditionally recommended (as per general ICE rules)
rule "Pneumococcal: if patient *will be* >= 5 years at the recommended due date and < 65 years and the series *is* complete, then recommend Conditional/High Risk" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "postRecommendationCheck"
	when
		There is a Series $targetSeries
			- a Forecast for the Series has been made and a Recommendation Date has been determined
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the Recommendation Date as $recommendationDate
			- post processing on the Series forecast has not already been run
		There exists an IceFact
			- that has finding SupportedFactConcept._PNEUMOCOCCAL_CHILD_SERIES_COMPLETE.conceptCodeValue
			- that has associated series $targetSeries
		There does not exist an IceFact
			- that has finding SupportedFactConcept._PNEUMOCOCCAL_RECOMMENDED_CHILD_RECEIVE_PCV13_DOSE.conceptCodeValue
			- that has associated series $targetSeries
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and $recommendationDate >= "5y" && elapsed time between $birthDate and $recommendationDate < "65y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE_HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END Recommendation Rules based on Age
/************************************************************************************************************************************************************************************/



/*************************************************************************************************************************************************************************************
Pneumococcal Child Series 4-dose Series Exception Rules
/************************************************************************************************************************************************************************************/

// Exception 1A
rule "Pneumococcal Child Series: Include Recommendation with Date 7months in 4-Dose PCV Series and TargetDose 2 if Patient is between 7and12months and Received No Doses before 7months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 500
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the effective dose number in the Series as $nEffectiveDoseNumber
			- the numeric $nEffectiveDoseNumber is <= 2
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeSevenMonths when the Patient is "7m" of age
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Number of Doses Administered before $dtDateAtAgeSevenMonths is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Skip Series Dose Number to 2 from $nEffectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeSevenMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 2 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 1B
rule "Pneumococcal Child Series: Include Recommendation with Date 7months in 4-Dose PCV Series and TargetDose 3 if Patient is between 7and12months and Received 1 Dose before 7months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 500
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the effective dose number in the Series as $nEffectiveDoseNumber
			- the numeric $nEffectiveDoseNumber is <= 3
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeSevenMonths when the Patient is "7m" of age
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Number of Doses Administered before $dtDateAtAgeSevenMonths is == 1
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Skip Series Dose Number to 3 from $nEffectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeSevenMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 3 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 2A
rule "Pneumococcal Child Series: Include Recommendation with Date 12months and TargetDose 3 in 4-Dose PCV Series if Patient between age 12and24months and Received <2 Doses before 12months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 500
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the effective dose number in the Series as $nEffectiveDoseNumber
			- the numeric $nEffectiveDoseNumber is <= 3
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwelveMonths when the Patient is "12m" of age	
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Number of Doses Administered before $dtDateAtAgeTwelveMonths is < 2
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "24m"
	then
		Skip Series Dose Number to 3 from $nEffectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeTwelveMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 3 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 2B
rule "Pneumococcal Child Series: Set Recommendation Date to 12months and TargetDose 4 in 4-Dose PCV Series and Patient between age 12and24months and Received 2 Doses before 12months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 500
	activation-group "recommendationAgeCheck"
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the effective dose number in the Series as $nEffectiveDoseNumber
			- the numeric $nEffectiveDoseNumber is <= 4
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwelveMonths when the Patient is "12m" of age
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Number of Doses Administered before $dtDateAtAgeTwelveMonths is == 2
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "24m"
	then
		Skip Series Dose Number to 4 from $nEffectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeTwelveMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 3
rule "Pneumococcal Child Series: Set Recommendation Date to 24months and TargetDose 4 in 4-Dose PCV Series if Patient between age 24months and 5years" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 500
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- make note of the effective dose number in the Series as $nEffectiveDoseNumber
			- the numeric $nEffectiveDoseNumber is <= 4
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwentyFourMonths when the Patient is "24m" of age					
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Effective Number of Doses in the Series before $dtDateAtAgeTwentyFourMonths is < 4
		Confirm elapsed time between $dtBirthDate and evalTime >= "24m" && elapsed time between $dtBirthDate and evalTime < "5y"
	then
		Skip Series Dose Number to 4 from $nEffectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeTwentyFourMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 4
rule "Pneumococcal Adult Series: Set Recommendation Date to 65y and Skip Dose to dose 6 (Adult Series target dose 1) if Patient >= 5y" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 500
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- make note of the effective dose number in the Series as $nEffectiveDoseNumber
			- the numeric $nEffectiveDoseNumber is <= 6
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeSixtyFiveYears when the Patient is "65y" of age					
		Confirm elapsed time between $dtBirthDate and evalTime >= "5y"
	then
		Skip Series Dose Number to 6 from $nEffectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeSixtyFiveYears for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 6 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END Pneumococcal Child Series 4-dose Series Exception Rules
/************************************************************************************************************************************************************************************/


