/**
 * Copyright (C) 2019 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package org.cdsframework.ice.v1_1_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseRule
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


// PCV shot administered between 5 years and 65 years is ACCEPTED / OUTSIDE_SERIES 
rule "Pneumococcal Adult Series: Evaluate PCV Shot >= 5yrs and < 65y as Accepted with reason code OUTSIDE_SERIES" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentDose that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the dose number in the Series is >= 6
			- the vaccine administered a member of ("ICE133", "ICE152")
			- make note of the date this shot was Administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationDate >= "5y"
		Confirm elapsed time between $dtBirthDate and $administrationDate < "65y"
	then
		Include the reason for shot $currentDose Accepted due to "Shot Administered Outside of a Series"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $associatedTargetSeries
end


// If a PCV is administered after 19 years, then override the default extra dose rule check - PCV shots should not be marked as an EXTRA_DOSE in this circumstance
rule "Pneumococcal Adult Series: Do _not_ mark PCV shots that have been evaluated as ACCEPTED/OUTSIDE_SERIES also as ACCEPTED/EXTRA_DOSE" 
		extends "Pneumococcal Adult Series: Evaluate PCV Shot >= 5yrs and < 65y as Accepted with reason code OUTSIDE_SERIES" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		// Nothing to do
	then
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $associatedTargetSeries
end


// For Adult Series: Identified (for Abstract) that PPSV shot was administered >= 5 years and < 65y-4d
rule "Pneumococcal Adult Series: (Abstract) PPSV Shot (CVX 33, 109) >= 5yrs and < 65y-4d (below minimum age)" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	when
		There is an Administered Shot $adultPPSVShot1BelowMinAge
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the dose number in the Series is == 6
			- the vaccine administered a member of ("ICE33", "ICE109")
			- make note of the date this shot was Administered as $adultPPSVShot1Date
			- make note of the associated series as $targetSeriesForAgeCheck
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $adultPPSVShot1Date >= "5y"
		Confirm elapsed time between $dtBirthDate and $adultPPSVShot1Date < "65y-4d"		
	then
		Record that this dose rule was processed for the TargetDose $adultPPSVShot1BelowMinAge
		Log that this dose rule fired for the dose $adultPPSVShot1BelowMinAge in the Series $targetSeriesForAgeCheck
end


// For Adult Series: A PPSV shot administered >= 5 years and < 65y-4d is ACCEPTED / BELOW_REC_AGE_SERIES
rule "Pneumococcal Adult Series: Evaluate target dose 1 PPSV Shot as Accepted with reason code BELOW_REC_AGE_SERIES" extends 
		"Pneumococcal Adult Series: (Abstract) PPSV Shot (CVX 33, 109) >= 5yrs and < 65y-4d (below minimum age)" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentShotForAgeCheck that needs to be evaluated
			- that is the same shot as $adultPPSVShot1BelowMinAge
	then
		Include the reason for shot $currentShotForAgeCheck Accepted due to "Below Recommended Age"
		Record that this dose rule was processed for the TargetDose $currentShotForAgeCheck
		Log that this dose rule fired for the dose $currentShotForAgeCheck in the Series $targetSeriesForAgeCheck
end


// For Adult Series: The minimum interval from a prior (non-valid) PPSV >= 5yrs and < 65y-4d to the next PPSV (target dose in the adult series) must be >= 5 years
rule "Pneumococcal Adult Series: Absolute minimum interval from the accepted dose 1 of PPSV to PPSV target dose 1 in the adult series = 5 years" extends 
		"Pneumococcal Adult Series: (Abstract) PPSV Shot (CVX 33, 109) >= 5yrs and < 65y-4d (below minimum age)" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"		
	activation-group "doseIntervalCheck"
	when
		There is an Administered Shot $currentShotForIntervalCheck that needs to be evaluated
			- the shot belongs to the Series $targetSeriesForAgeCheck
			- the vaccine administered a member of ("ICE33", "ICE109")
			- make note of the administered shot number as $nShotNumber
			- make note of the date this shot was Administered as $adultPPSVShot2Date
		There is an Administered Shot $previousShot
			- that is the same shot as $adultPPSVShot1BelowMinAge
			- the administered shot number is < $nShotNumber
		Confirm Elapsed time between $adultPPSVShot1Date and $adultPPSVShot2Date < "5y"
	then
		Include the reason for shot $currentShotForIntervalCheck Invalid due to "Below Minimum Interval"
		Record that this dose rule was processed for the TargetDose $currentShotForIntervalCheck
		Log that this dose rule fired for the dose $currentShotForIntervalCheck in the Series $targetSeriesForAgeCheck
end


/////////////////////
// For the Adult Series -  If a PPSV administered first and then a PCV, or PCV first and then PPSV, the absolute interval from the accepted PPSV to PCV (target dose in the adult series) = 0 days. 
// If the PCV is administered first, the absolute minimum interval from dose 5 to dose 6 is already set to 0 in the supporting data, so nothing needs to be done in this scenario.
/////////////////////

// For Adult Series: If a patient's age at administration is >= 65 years, then a PCV shot (CVX 133 or 152) is evaluated as Accepted and the reason code is CLINICAL_PATIENT_DISCRETION. Include this in the
// "extraDoseCheck" activation-group, in case the series is complete. We do not want the default rule to fire which would mark this as an extra dose.
rule "Pneumococcal Adult Series: If a patient's age at administration is >= 65 years, then a PCV shot (CVX 133 or 152) is evaluated as Accepted and the reason code is CLINICAL_PATIENT_DISCRETION" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the dose number in the Series is >= 6
			- the vaccine administered a member of ("ICE133", "ICE152")
			- make note of the date this shot was Administered as $dtShotAdministered
			- make note of the associated series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm Elapsed time between $dtBirthDate and $dtShotAdministered >= "65y"
	then
		Include the reason for shot $currentShot Accepted due to "Clinical Patient Discretion"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


/////////////////////
// Adult Series Rules for CVX 100 (PCV 7)
//     1) If CVX 100 (PCV 7) is reported, then evaluate as Accepted and the reason code is VACCINE_NOT_ALLOWED. 
//     2) Ignore the reported CVX 100 when determining the recommended vaccine and calculating the recommended due date for the next target dose.
// Adult Series: If PCV 7 is administered in the Child Series, then mark the shot as *Accepted* (rather than INVALID) with a reason code of VACCINE_NOT_ALLOWED.
//
// Adult Series: PCV 7 Rule #1 - this vaccine allowable check must occur before the other vaccine allowable checks (accomplished via salience)
rule "Pneumococcal Adult Series: Evaluate PCV 7 shot (CVX 100) as Accepted with a reason code of VACCINE_NOT_ALLOWED" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	salience 100
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the vaccine administered is "ICE100"
			- the dose number in the Series is >= 6
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Allowed"
		Mark that evaluation of shot $currentShot is complete and therefore should not be reevaluated by any other rules
		Record that this dose rule was processed for the TargetDose $currentShot
end


// Adult Series: PCV 7 Rule #2
rule "Pneumococcal Adult Series: Note that the Absolute Minimum interval from PCV 7 = 0d by marking the shot Ignored" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 101
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the vaccine administered is "ICE100"
			- the dose number in the Series is >= 6
	then
		Mark the shot $currentShot as Ignored
		Record that this dose rule was processed for the TargetDose $currentShot
end


/////////////////////
// Child Series: If an invalid vaccine is administered in the Child Series, mark the shot as *Accepted* (rather than INVALID) with a reason code of VACCINE_NOT_PART_OF_THIS_SERIES. 
// As of 10/2016, only CVX 33. (Overrides general "allowableVaccineCheck" rule)
/////////////////////
rule "Pneumococcal Child Series: Evaluate shot administered with an invalid vaccine as Accepted with reason code of VACCINE_NOT_PART_OF_THIS_SERIES" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- make note of the administered vaccine as $vaccineAdministered
			- make note of the dose number as $doseNumber
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the series is <= 5
			- the vaccine $vaccineAdministered is not permitted for dose number $doseNumber in this series
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Part of This Series"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// Child Series: Minimum Interval the minimum interval between a PPSV23 (CVX 33) in Child Series and a dose of PCV (CVX 100, 133, 152, 109) is 0 days.
// (Since the PPSV shot is invalid, simply mark it as ignored)
rule "Pneumococcal Child Series: Evaluate the minimum interval as 0 days between a PPSV23 (CVX 33) and a shot of PCV (CVX 100, 133, 152, 109) administered at < 5yrs of age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseIntervalCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the administered shot number is > 1
			- the vaccine administered a member of ("ICE100", "ICE133", "ICE152", "ICE109")
			- make note of the administered shot number as $nShotNumber
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
		There is an administered shot $previousShot
			- the shot belongs to the series $associatedTargetSeries
			- the administered shot number is <= $nShotNumber-1
			- the vaccine administered is "ICE33"
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the effective dose number in the series is <= 5
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationDate < "5y"
	then
		Mark the shot $previousShot as Ignored
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


/////////////////////
// Child Series: If a shot is administered < 5 years of age and is an extra dose (more than 4), mark the shot as ACCEPTED and EXTRA_DOSE. Note that the PCV13 rule (below) 
// must execute before this one. (This rule is needed because Child and Adult series have been combined into one, so base rules will not automatically mark the shot as an EXTRA shot)
/////////////////////
rule "Pneumococcal Child Series: Evaluate extra shots before 5 yrs of age as ACCEPTED / EXTRA_DOSE" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the dose number in the series is > 4
			- make note of the Date this Shot was Administered as $administrationDate
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationDate < "5y"
	then
		Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Mark that evaluation of shot $currentShot is complete and therefore should not be reevaluated by any other rules
		Record that this dose rule was processed for the TargetDose $currentShot
end


/////////////////////
// If the patient has had 4 effective doses before 5 years of age, make note of the fact that the childhood portion of this series is complete via an IceFact
/////////////////////
rule "Pneumococcal: Note completion of child series if the patient has received 4 effective doses prior to 5 years of age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "postEvaluationCheck"
	when
		There is an administered shot $doseThatCompletesChildSeries
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the dose number in the series is == 4
			- that has already been evaluated and whose Shot Validity is VALID
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $targetSeries
		There does not exist an IceFact
			- that has finding SupportedFactConcept._PNEUMOCOCCAL_CHILD_SERIES_COMPLETE.conceptCodeValue
			- that has associated series $targetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt5yOfAge when the patient is "5y" of age
		There is an administered shot $matchingDose
			- that is the same shot as $doseThatCompletesChildSeries
			- the administration date of the shot is < $dateAt5yOfAge
	then
 		Insert an IceFact SupportedFactConcept._PNEUMOCOCCAL_CHILD_SERIES_COMPLETE.getConceptCodeValue() with TargetSeries $targetSeries into working memory
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
PCV 13 Rule: If a child is complete for the Child series, has not had a dose of PCV 13 (CVX 133), is currently < 5 years old, and will be < 5 years old at the recommended due date, 
then a dose of PCV 13 (CVX 133) is recommended. Recommended interval is 8 weeks
/************************************************************************************************************************************************************************************/

// Abstract rule for PCV13 rule: patient < 5yrs of age, series is complete and no prior PCV shots until the current shot under evaluation
rule "Pneumococcal(Abstract): Child Series- PCV Shot is need for patient < 5yrs where Child Series is otherwise complete but there are no prior PCV doses" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the vaccine administered is "ICE133"
			- make note of the administered shot number as $nShotNumber
			- make note of the Date this Shot was Administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
		There is an administered shot $previousShot
			- the shot belongs to the series $associatedTargetSeries
			- the administered shot number is == $nShotNumber-1
			- make note of the Date this Shot was Administered as $previousAdministrationDate
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the series is == 5
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Verify that the Count of Doses Administered in Series $targetSeries with Vaccine "ICE133" is == 0
		Confirm elapsed time between $dtBirthDate and $administrationDate < "5y"
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// If a child is already complete for the series, has not had a dose of PCV 13 (CVX 133), is currently < 5 yrs old in the Child Series, then this PCV 13 shot 
// is evaluated to VALID as long as an interval of 52 days has elapsed since last shot.
rule "Pneumococcal Child Series: Evaluate PCV Shot as Valid if Patient < 5yrs, interval is >= 52 days and Series is Complete" extends 
		"Pneumococcal(Abstract): Child Series- PCV Shot is need for patient < 5yrs where Child Series is otherwise complete but there are no prior PCV doses" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	salience 50
	when
		Confirm elapsed time between $previousAdministrationDate and $administrationDate >= "52d"
	then
		Include the reason for shot $currentShotValid Valid
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// If a child is already complete for the series, and this shot is PCV 13 (CVX 133), is currently < 5 yrs old, then this PCV 13 shot is evaluated to INVALID if an interval 
// of 52 days has not elapsed since last shot.
rule "Pneumococcal Child Series: Evaluate PCV Shot as Invalid if Patient < 5yrs, interval is < 52 days and Series is Complete" extends 
		"Pneumococcal(Abstract): Child Series- PCV Shot is need for patient < 5yrs where Child Series is otherwise complete but there are no prior PCV doses" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	salience 50
	when
		Confirm elapsed time between $previousAdministrationDate and $administrationDate < "52d"
	then
		Include the reason for shot $currentShot Invalid due to "Below Minimum Interval"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end
	
/*************************************************************************************************************************************************************************************
 END PCV 13 Rule
/************************************************************************************************************************************************************************************/


rule "Pneumococcal Child Series: Exception 1: Evaluate PCV Shot as Below Minimum Age for Final Dose if 4th Dose Administered < the Absolute Minimum Age (1y-4d) and Received 0 Doses Administered Prior to 7m of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when		
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the Dose Number in the Series is == 4
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
			- make note of the Absolute Minimum Age of this dose as $absMinAgeDose4
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "7m" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Doses Administered before $dateOfInterest is == 0			
		/////// Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "361d"
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate < $absMinAgeDose4
	then
		Include the reason for shot $currentDose Invalid due to "Below Minimum Age for Final Dose"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 1A: Set Series Dose Number to 2 if No Doses Administered and Patient >= 7m and < 12 Months at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Administered Shots is == 0
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is < 2
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Skip Series Dose Number to 2 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "Pneumococcal Child Series: Exception 1A: Skip Dose Number to 2 for Shot Administered to Patient between >= 7m and < 12m" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when		
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with Name "PneumococcalSeries"
			- the Dose Number in the Series is < 2
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "7m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "12m"
	then
		Set Dose Number of $currentDose to 2
		Skip Series Dose Number to 2 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end


rule "Pneumococcal Child Series: Exception 1B: Skip Dose Number to 3 if received Dose Administered prior to 7m and Shot being evaluated for Patient between >= 7m and < 12m" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when		
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with Name "PneumococcalSeries"
			- the Dose Number in the Series is == 2
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		There is an Administered Shot $currentDosePrior distinct from $currentDose
			- the shot belongs to the Series $associatedTargetSeries
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with Name "PneumococcalSeries"
			- the Dose Number in the Series is == 1
			- that has already been evaluated and whose Shot Validity is VALID
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDatePrior
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDatePrior < "7m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "7m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "12m"
	then
		Set Dose Number of $currentDose to 3
		Skip Series Dose Number to 3 from 2 for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end


rule "Pneumococcal Child Series: Exception 2: Set Series Dose Number to 3 if No Shots Administered and Patient >= 12m and < 24m of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when		
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Administered Shots is == 0
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is < 3
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "24m"	
	then
		Skip Series Dose Number to 3 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 2: Skip Dose Number to 3 if Patient Between >= 12m and < 24m at Shot Date and has Received <2 Doses Administered at <12m of Age at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with Name "PneumococcalSeries"
			- the Dose Number in the Series is < 3
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Doses Administered before $dateOfInterest is < 2			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "24m"
	then
		Set Dose Number of $currentDose to 3
		// Pneumoccocal pneumonia
		Skip Series Dose Number to 3 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 2: Skip Dose Number to 4 if Patient Between >= 12m and < 24m and has Received 2 Doses Administered at <12m of Age at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when		
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with Name "PneumococcalSeries"
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Doses Administered before $dateOfInterest is == 2
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "24m"
	then
		Set Dose Number of $currentDose to 4
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 3: Set Series Dose Number to 4 if No Shots Administered and Patient >= 24m and < 5y of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when		
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Administered Shots is == 0
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is < 4
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "24m" && elapsed time between $dtBirthDate and evalTime < "5y"	
	then
		Skip Series Dose Number to 4 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 3: Skip Dose Number to 4 for Patient >= 24m and < 5y at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when		
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with Name "PneumococcalSeries"
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "24m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "5y"
	then
		Set Dose Number of $currentDose to 4
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end


rule "Pneumococcal Adult Series: Exception 4: Set Series Dose Number to 6 (AdultSeries) if No Shots Administered and Patient >= 5y of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Administered Shots is == 0
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is < 6
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "5y"
	then
		Skip Series Dose Number to 6 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 4: Skip Dose Number to 6 (Adult Series) for Patient >= 5y at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with Name "PneumococcalSeries"
			- the Dose Number in the Series is < 6
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "5y"
	then
		Set Dose Number of $currentDose to 6
		Skip Series Dose Number to 6 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end
